//****************************************************************************
// $Module type: Selection list
// $Module name: HolidaysLookup.pkg
// $Author     : Nils Svedmyr
// Created     : 2012-03-09 @ 16:23
//
// Description : Dynamic lookup list to display holidays - and to make multi-selections from.
//
// Note        : Change the 'Use AmericanHolidays.pkg' to your language holiday package file.
//               If your country is not available as a country holiday package it is easy to make one.
//               Just look at the other country holiday packages and make your own from those samples.
//
// $Rev History:
//    2012-03-09 Module header created
//    2025-11-22 Moved definitions at top of package to include file.
//
//****************************************************************************
Use HolidaysLookup.h.pkg

// See bottom of package for General purpose access method!
//   Function PopupHolidayLookup String sISO_Short Returns tHoliday[]
Object oHolidaysLookup is a cRDCModalPanel
    Set Size to 209 372
    Set Label to CS_LookupHeader
    Set piMinSize to 89 211
    Set Location to 2 2
    Set Border_Style to Border_Thick
    Set Icon to "NationalHolidays.ico"

    Property Boolean pbOk False
    Property tHoliday[] pSelectedHolidaysArray
    
    Object oHolidayPromptList is a cRDCCJGridPromptList
        Set Size to 167 362
        Set Location to 20 5 

        Object oDate_col is a cCJGridColumn
            Set piWidth to 90
            Set psCaption to CS_WeekDateCaption
            Set peDataType to Date_Window
            Set peTextAlignment to xtpAlignmentCenter
            Set peHeaderAlignment to xtpAlignmentCenter
        End_Object

        Object oDescription_col is a cCJGridColumn
            Set piWidth to 235
            Set psCaption to CS_NameCaption
            Set peHeaderAlignment to xtpAlignmentCenter
        End_Object

        Object oWeekday_col is a cCJGridColumn
            Set piWidth to 85
            Set psCaption to CS_WeekDayNameCaption
            Set psToolTip to "Local computer date settings are used here."
            Set peHeaderAlignment to xtpAlignmentCenter
            Set peTextAlignment to xtpAlignmentCenter
        End_Object

        Object oHoliday_col is a cCJGridColumn
            Set piWidth to 83
            Set psCaption to CS_HolidayNameCaption
            Set psToolTip to CS_HolidayNameTooltip
            Set pbCheckbox to True
            Set peHeaderAlignment to xtpAlignmentCenter
        End_Object

        Object oWeekNumber_col is a cCJGridColumn
            Set piWidth to 87
            Set psCaption to CS_WeekNumberCaption
            Set peTextAlignment to xtpAlignmentCenter
            Set peHeaderAlignment to xtpAlignmentCenter
            Set peDataType to Mask_Numeric_Window
        End_Object

        Object oDateDayNumber_col is a cCJGridColumn
            Set piWidth to 69
            Set psCaption to CS_DayNumberCaption
            Set peTextAlignment to xtpAlignmentCenter
            Set peHeaderAlignment to xtpAlignmentCenter
            Set peDataType to Mask_Numeric_Window
        End_Object

        Procedure LoadData
            Handle hoDataSource
            tDataSourceRow[] TheData
            Boolean bHoliday bLeap
            Integer i iRows iDate_col iDescription_col iWeekday_col iHoliday_col
            Integer iWeekNumber_col iWeekNo iDayNumber_col
            Date dYearDate
            String sHolidayName sWeekDay
            Integer iYear iDays
            DateTime dtVar

            Send Cursor_Wait of Cursor_Control

            Get Value of oYear_sf to iYear
            If (iYear < 1) Begin
                Move (DateGetYear(CurrentDateTime())) to iYear
                Set Value of oYear_sf to iYear
            End

            Get IsLeapYear of ghoCalendarHolidays iYear to bLeap
            If (bLeap = True) Begin
                Move 366 to iDays
            End
            Else Begin
                Move 365 to iDays
            End

            Get phoDataSource to hoDataSource

            // Get the datasource indexes of the various columns
            Get piColumnId of oDate_col        to iDate_col
            Get piColumnId of oDescription_col to iDescription_col
            Get piColumnId of oWeekday_col     to iWeekday_col
            Get piColumnId of oHoliday_col     to iHoliday_col
            Get piColumnId of oWeekNumber_col  to iWeekNumber_col
            Get piColumnId of oDateDayNumber_col to iDayNumber_col
            // Load all data into the datasource array
            Get DateSetDate of ghoCalendarHolidays iYear 1 1 to dYearDate

            For i From 1 to iDays
                Get IsHoliday of ghoNationalHolidayObject dYearDate (&bHoliday) to sHolidayName
                If (sHolidayName <> "") Begin
                    Get DateGetDayName of ghoCalendarHolidays dYearDate to sWeekDay
                    Get DateGetISOWeek of ghoCalendarHolidays dYearDate to iWeekNo
                    Move dYearDate    to TheData[iRows].sValue[iDate_col]
                    Move sHolidayName to TheData[iRows].sValue[iDescription_col]
                    Move (Trim(sWeekDay)) to TheData[iRows].sValue[iWeekday_col]
                    Move bHoliday     to TheData[iRows].sValue[iHoliday_col]
                    Move iWeekNo      to TheData[iRows].sValue[iWeekNumber_col]
                    Move dYearDate to dtVar
                    Move (DateGetDayOfYear(dtVar)) to TheData[iRows].sValue[iDayNumber_col]

                    Increment iRows
                End
                Increment dYearDate
            Loop

            Send ReInitializeData TheData True
            Send MovetoFirstRow
            Send Cursor_Ready of Cursor_Control
        End_Procedure

        Function GetSelectedHolidays Returns tHoliday[]
            tDataSourceRow[] TheData
            Handle hoDataSource
            Integer[] ArrayOfSelected
            tHoliday[] SelectedHolidays
            Integer iDate_col iDescription_col  iHoliday_col
            Integer iCount iSize iRow

            // Get the datasource indexes of the various columns
            Get piColumnId of oDate_col        to iDate_col
            Get piColumnId of oDescription_col to iDescription_col
            Get piColumnId of oHoliday_col     to iHoliday_col

            Get phoDataSource to hoDataSource
            Get DataSource of hoDataSource to TheData
            Get GetIndexesForSelectedRows to ArrayOfSelected
            Move (SizeOfArray(ArrayOfSelected)) to iSize
            Decrement iSize

            For iCount from 0 to iSize
                Move ArrayOfSelected[iCount] to iRow
                Move TheData[iRow].sValue[iDate_col]        to SelectedHolidays[iCount].dDate
                Move TheData[iRow].sValue[iDescription_col] to SelectedHolidays[iCount].sHolidayName
                Move TheData[iRow].sValue[iHoliday_col]     to SelectedHolidays[iCount].bNationalHoliday
            Loop
            Function_Return SelectedHolidays
        End_Function

        Procedure OnCreate
            Forward Send OnCreate
            Send LoadData
        End_Procedure

        Procedure OnMoveValueOutByValue
            Send KeyAction of oOK_Btn
        End_Procedure

    End_Object

    Object oOK_Btn is a Button
        Set Label    to CS_OkButtonCaption
        Set Location to 192 264
        Set peAnchors to anBottomRight
        Procedure OnClick
            tHoliday[] SelectedHolidaysArray
            Set pbOk to True
            Get GetSelectedHolidays of oHolidayPromptList to SelectedHolidaysArray
            Set pSelectedHolidaysArray to SelectedHolidaysArray
            Send Close_Panel
        End_Procedure
        On_Key Key_Escape Send KeyAction of oCancel_Btn
    End_Object

    Object oCancel_Btn is a Button
        Set Label    to CS_CancelButtonCaption
        Set Location to 192 318
        Set peAnchors to anBottomRight
        Procedure OnClick
            Send Close_Panel
        End_Procedure
        On_Key Key_Escape Send KeyAction
    End_Object

    Object oInfo_tb is a TextBox
        Set Auto_Size_State to False
        Set Size to 16 237
        Set Location to 190 5
        Set Label to CS_ShortCutKeysInfoText
        Set peAnchors to anBottomLeft
        Set Justification_Mode to JMode_Left
    End_Object

    Object oYear_sf is a SpinForm
        Set Size to 13 43
        Set Location to 4 79
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Minimum_Position to 1800
        Set Label to CS_SpinFormLabel
        Set psToolTip to CS_SpinformInfoText
        Procedure OnChange
            Integer iYear
            Get Value to iYear
            If (IsComObjectCreated(oHolidayPromptList) and iYear >= 1800) Begin
                Send LoadData of oHolidayPromptList
            End
        End_Procedure
        Procedure OnStartup
            Integer iYear
            Move (DateGetYear(CurrentDateTime())) to iYear
            Set Value to iYear
        End_Procedure
        Send OnStartup
        On_Key Key_Escape Send KeyAction of oCancel_Btn
    End_Object

    Function GetHolidayData tHoliday[] ByRef HolidaysArray Returns Boolean
        Boolean bOk
        tHoliday[] SelectedHolidays

        Set pbOk to False
        Send Popup
        Get pbOk to bOk

        If (bOk = True) Begin
            Get pSelectedHolidaysArray to SelectedHolidays
            Move SelectedHolidays to HolidaysArray
        End
        Function_Return bOk
    End_Function

    On_Key Key_Alt+Key_O Send KeyAction of oOK_Btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_Btn
End_Object


// *** Public access message ***
Function PopupHolidayLookup String sISO_Short Returns tHoliday[]
    Handle hoHolidaysObject ho
    tHoliday[] SelectedHolidays
    
    Move (oHolidaysLookup(Self)) to ho   
    Get NationalHolidaysObject of ghoCalendarHolidays sISO_Short to hoHolidaysObject
    Move hoHolidaysObject to ghoNationalHolidayObject
    Send Popup of ho
    Get pSelectedHolidaysArray of ho to SelectedHolidays
    
    Function_Return SelectedHolidays
End_Function
