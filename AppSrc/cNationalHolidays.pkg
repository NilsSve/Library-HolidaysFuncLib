// This is the class to use when creating National Holidays packages,
// such as BrittishHolidays.pkg, SwedishHolidays.pkg, NorwegianHolidays.pkg etc.
// Note: Each country holiday package based on this class should be added 
//       to the "CountryPackages.pkg."
// The class registers itself with the ghoCalendarHolidays object.

Use cCalendarHolidays.pkg

Class cNationalHolidays is a cObject
    Procedure Construct_Object
        Forward Send Construct_Object
        Move Self to ghoNationalHolidayObject

        { Visibility = Private }
        Property String private_psISO_Short
        { Visibility = Public }
        Property String psOfficial_Short
    End_Procedure
    
    { MethodType = Property }    
    Procedure Set psISO_Short String sISO_Short
        Set private_psISO_Short to sISO_Short
        Move sISO_Short to Nations.ISO_Short
        Find EQ Nations.ISO_Short
        If (Found) Begin
            Set psOfficial_Short to (Trim(Nations.Official_Short))
        End
    End_Procedure  
    
    Function psISO_Short Returns String
        Function_Return (private_psISO_Short(Self))
    End_Function

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        If (ghoCalendarHolidays <> 0) Begin
            Send RegisterNationalHolidays of ghoCalendarHolidays Self
        End
    End_Procedure

End_Class
