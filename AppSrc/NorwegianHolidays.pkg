//****************************************************************************
// $Module type: Package
// $Module name: NorwegianHolidays.pkg
// $Author     : Ola Eldoy
// Created     : 2011-02-22 @ 08:00
//
// Description : Function that returns the name of Norwegian holidays and special days from a date variable.
//
// Note        : To create a similar package for another country, copy the function below and
//               simply change the name and wordings and possibly also what should constitute the holidays
//               for your country. All the underlying holiday functions can be found in oCalendarHolidays.pkg
//               The HolidayName function is used by the Holidays.vw sample view; the oCreateDates_bp business process.
//
// $Rev History:
//    2010-03-22 Module header created
//
//    2010-03-30 Added functionality to make it easier to add just about any 'special day'
//               that you might want to add to a Is????Holiday function. (were ???? is the country name)
//               Look for comment: *** New 2010-03-30 NGS ***
//
//
//****************************************************************************
Use cNationalHolidays.pkg

Object oNorwegianHolidays is a cNationalHolidays
    Move Self to ghoNationalHolidayObject

    // A 2-character ISO_Short string, according to the list:
    // https://www.iso.org/obp/ui/#search/code/
    Set psISO_Short to "NO"

    // Function that returns the name of Norwegian 'holidays' from a date variable.
    //
    // Function HolidayName takes two parameters;
    // A 4-year digit date variable and a boolean as a ByRef variable.
    //
    // Returns: A string with the 'holidayname' as text AND it changes the boolean ByRef
    // variable if the returned holiday is considered to be a national holiday - these
    // national holidays are regulated by law. So if the boolean is false the date
    // is really not a holiday at all - but rather some sort of 'special' date that one
    // wants to have included in the functionality.
    //
    // For example; Christmas Eve is a "holiday" but not a national holiday (regulated by law),
    // In contrast Christmas Day is a national holiday and so the ByRef bOfficialHoliday will be TRUE for Christmas Day,
    // but false for Christmas Eve.
    //
    // To include your own special days see *** New 2010-03-30 NGS *** below for an example
    // on how to add e.g. the "International Women's Day" wich falls on the 8th of March
    // each year. It's really simple to do.
    //
    // Note that although all functions below defines two parameters, not all
    // of those functions use the second parameter.
    Function HolidayName Date dInDate Boolean ByRef bOfficialHoliday Returns String
        Integer iYear
        Date dHolidayDate
        String sHolidayName sISO_Short

        Move "" to sHolidayName
        Move False to bOfficialHoliday
        Get psISO_Short to sISO_Short

        If (dInDate < 1) Begin
            Function_Return ""
        End
        Move (DateGetYear(dInDate)) to iYear

        // *** Fixed Holidays ***

        Get NewYearsEve of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "nyttårsaften"
        End

        Get NewYearsDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "1. nyttårsdag"
        End

        Get LaborDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "offentlig høytidsdag"
        End

        Get ChristmasEve of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "julaften"
        End

        Get ChristmasDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "1. juledag"
        End

        Get SecondDayChristmas of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "2. juledag"
        End

        Get NationalDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "grunnlovsdag"
        End

        // This is an example on how to add a 'holiday'
        // for any special day that you might
        // want to include in a e.g. holidays date table.
        // The International Women's Day falls each year on the 8th of March.
        If ((DateGetMonth(dInDate) = 3) and (DateGetDay(dInDate) = 8)) Begin  // 8th of March.
            Move False to bOfficialHoliday
            Function_Return "kvinnedagen" // "International Women's Day"
        End

        If ((DateGetMonth(dInDate) = 5) and (DateGetDay(dInDate) = 8)) Begin  // 8th of May: Liberation day
            Move False to bOfficialHoliday
            Function_Return "frigjøringsdag"
        End

        // *** Movable holidays ***
        // Easter functions at the bottom; they are the most complicated to calculate.

        Get PalmSunday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "palmesøndag"
        End

        Get AscensionDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "Kristi Himmelfartsdag"
        End

        Get WhitSunday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "1. pinsedag"
        End

        Get WhitMonday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "2. pinsedag"
        End

        Get MidsummerDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "sankthans"
        End

        Get MaundyThursday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "skjærtorsdag"
        End

        Get GoodFriday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "langfredag"
        End

        Get HolySaturday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "påskeaften"
        End

        Get EasterDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "1. påskedag"
        End

        Get EasterMonday of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move True to bOfficialHoliday
            Function_Return "2. påskedag"
        End

        // Vårdagjämning
        Get VernalEquinox of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "Vårjevndøgn"
        End
        
        // Höstdagjämning
        Get AutumnalEquinox of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "Høstjevndøgn"
        End
        
        // This needs to be after all easter days.
        // So when April fools day falls on one of the easter days,
        // an easter day will be returned instead. (More imporant)
        Get AprilFoolsDay of ghoCalendarHolidays iYear sISO_Short to dHolidayDate
        If (dInDate = dHolidayDate) Begin
            Move False to bOfficialHoliday
            Function_Return "1. april"
        End

        Function_Return sHolidayName
    End_Function
End_Object
